plugins {
    id 'java'
    id 'application'
    id 'com.palantir.git-version' version '0.15.0'
}

group 'com.github.ianparkinson'
version gitVersion()

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.10'
    implementation 'info.picocli:picocli:4.7.0'
    annotationProcessor 'info.picocli:picocli-codegen:4.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testImplementation 'com.google.truth:truth:1.1.3'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

task copyLicense(type: Copy) {
    group = "build"
    description = "Copy legal docs into the build"
    from layout.projectDirectory.file("LICENSE"), layout.projectDirectory.file("NOTICE")
    into new File(sourceSets.main.output.resourcesDir, "META-INF")
}

compileJava {
    sourceCompatibility = 11
    targetCompatibility = 11
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
    dependsOn(copyLicense)
}

application {
    mainClass = 'com.github.ianparkinson.helog.Helog'
}

test {
    useJUnitPlatform()
    sourceCompatibility = 11
    targetCompatibility = 11
}